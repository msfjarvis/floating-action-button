/*
 * Copyright © 2014 Jerzy Chalupski
 * Copyright © 2018 Jason A. Donenfeld <Jason@zx2c4.com>
 * Copyright © 2018-2019 Harsh Shandilya <msfjarvis@gmail.com> All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */
plugins {
    id 'kotlin-android'
    id 'digital.wup.android-maven-publish' version '3.6.2'
}

final def gitHash = { ->
    final def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    stdout.toString().trim()
}

final def keystorePropertiesFile = rootProject.file 'keystore.properties'

android {
    defaultConfig {
        consumerProguardFiles 'consumer-proguard-rules.pro'
        versionName "$VERSION_NAME"
    }
    if (keystorePropertiesFile.exists()) {
        final def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
        signingConfigs {
            release {
                keyAlias = keystoreProperties['keyAlias']
                keyPassword = keystoreProperties['keyPassword']
                storeFile = rootProject.file keystoreProperties['storeFile']
                storePassword = keystoreProperties['storePassword']
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }
    lintOptions {
        abortOnError = false
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:' + versions.appcompat
    api 'com.google.android.material:material:' + versions.material
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:' + versions.kotlin
}

task androidSourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/msfjarvis/floating-action-button')
            credentials {
                username = System.getenv('USERNAME')
                password = System.getenv('PASSWORD')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.android
            groupId "$GROUP_ID"
            artifactId 'floating-action-button'
            version System.env['RELEASE'] != null ? "$VERSION_NAME" : "${gitHash()}-SNAPSHOT"
        }
    }
}
